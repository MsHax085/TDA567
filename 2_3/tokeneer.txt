 class Token{
	var fingerprint : int;
	var securityclearance : int;
	var valid : bool;
	
	constructor init(finger : int, clearance : int)
		modifies this;
		ensures fingerprint == finger;
		ensures securityclearance == clearance;
		ensures valid == true;
		{
			valid := true;
			fingerprint := finger;
			securityclearance := clearance;
		}
	
	method cancelToken()
		modifies `valid;
		requires valid == true;
		ensures valid == false;
		{
			valid := false;
		}
}

class EnrollmentStation{
	var enrolledUsers : map<int, User>;
	
	method init()
		modifies this;
		ensures |enrolledUsers| == 0;
		{
			enrolledUsers := map[];
		}
		
	method createUser(fingerprint : int, clearance : int) returns (user : User)
		modifies `enrolledUsers;
		requires fingerprint !in enrolledUsers;
		requires clearance == 1 || clearance == 2 || clearance ==3;
    
		ensures enrolledUsers == old(enrolledUsers)[fingerprint := user];
		ensures fingerprint in enrolledUsers;
		ensures enrolledUsers[fingerprint] == user;
		ensures user != null;
		ensures user.fingerprint == fingerprint;
    ensures fresh(user.token);
		ensures user.token != null;
		ensures user.token.valid == true;
		ensures user.token.fingerprint == fingerprint;
		ensures user.token.securityclearance == clearance;
		{
    		var newToken := new Token.init(fingerprint, clearance);
    		var newUser := new User.init(fingerprint, newToken);
    		enrolledUsers := enrolledUsers[fingerprint := newUser];
    		return newUser; 
		}
}


class IDStation{
	var requiredsecurity : int;
	var alarmState : bool;
	var doorState : bool;
	
	constructor init(requiredsecurity : int)
		modifies this;
		requires requiredsecurity == 3 || requiredsecurity == 2 || requiredsecurity == 1;
		ensures this.requiredsecurity == requiredsecurity && !alarmState && !doorState;
		{
			this.requiredsecurity := requiredsecurity;
			alarmState := false;
			doorState := false;
		}
		
	method requestAccess(user : User, scannedFP : int) returns (valid : bool)
		modifies user.token`valid, `alarmState,`doorState;
		requires user != null;
		requires user.token != null;
		requires user.token.valid == true;
		ensures user.token.fingerprint!=scannedFP ==> !valid && !user.token.valid && alarmState && !doorState;
		ensures user.token.fingerprint==scannedFP && user.token.securityclearance >= requiredsecurity ==> valid && !alarmState && user.token.valid && doorState;
		ensures user.token.fingerprint==scannedFP && user.token.securityclearance < requiredsecurity ==> !valid && alarmState && !doorState && user.token.valid;
		{
 			var token := user.token;
				if (token.fingerprint == scannedFP) {
					if (token.securityclearance >= requiredsecurity) {
						alarmState := false;
						doorState := true;
						valid := true;
					} else {
					alarmState := true;
					doorState := false;
					valid := false;} 
				} else {
					user.token.valid := false;
					alarmState := true;
					doorState := false;
					valid := false;
				}
		}
  
	method closeDoor()
		modifies `doorState;
		requires doorState == true;
		ensures doorState == false;
		{
			doorState := false;
		}
}

class User{
	var fingerprint: int;
	var token: Token

	constructor init(fingerprint : int, t : Token)
		modifies this;
		ensures this.fingerprint == fingerprint;
		ensures this.token == t;
		{
			this.fingerprint := fingerprint;
			this.token := t;
		}
}

class Test {
	method main()
	{
    //Initialize variables
    var boolean : bool;
		var ERS := new EnrollmentStation.init();
		var user1LvL1 := ERS.createUser(1,1);
		var user2LvL2 := ERS.createUser(2,2);
		var user3LvL3 := ERS.createUser(3,3);
    var doorLvL1 := new IDStation.init(1);
		var doorLvL2 := new IDStation.init(2);
    var doorLvL3 := new IDStation.init(3);
    
    //Assert that user with securityClearance = 1 is enrolled and has correct values
		assert user1LvL1.fingerprint in ERS.enrolledUsers;
		assert user1LvL1.token.valid;
		assert user1LvL1.token.fingerprint == 1;
		assert user1LvL1.token.securityclearance == 1;
		assert user1LvL1.fingerprint == 1;
		
    //Assert that user with securityClearance = 2 is enrolled and has correct values
		assert user2LvL2.fingerprint in ERS.enrolledUsers;
		assert user2LvL2.token.valid;
		assert user2LvL2.token.fingerprint == 2;
		assert user2LvL2.token.securityclearance == 2;
		assert user2LvL2.fingerprint == 2;
    
		//Assert that user with securityClearance = 3 is enrolled and has correct values
		assert user3LvL3.fingerprint in ERS.enrolledUsers;
		assert user3LvL3.token.valid;
		assert user3LvL3.token.fingerprint == 3;
		assert user3LvL3.token.securityclearance == 3;
		assert user3LvL3.fingerprint == 3;
    
    //Assert that door with requiredSecurtiy = 1 is initialized correctly
    assert doorLvL1.requiredsecurity == 1;
    assert !doorLvL1.alarmState;
    assert !doorLvL1.doorState;
    
    //Assert that door with requiredSecurtiy = 2 is initialized correctly
    assert doorLvL2.requiredsecurity == 2;
    assert !doorLvL2.alarmState;
    assert !doorLvL2.doorState;
    
    //Assert that door with requiredSecurtiy = 3 is initialized correctly
    assert doorLvL3.requiredsecurity == 3;
    assert !doorLvL3.alarmState;
    assert !doorLvL3.doorState;
    
    //Assert that user with securityClearance = 1 can pass through door with requiredSecurity = 1
    boolean := doorLvL1.requestAccess(user1LvL1, 1);
    assert boolean;
    assert doorLvL1.doorState;
    assert !doorLvL1.alarmState;
    doorLvL1.closeDoor();
    assert !doorLvL1.doorState;
    
    //Assert that user with securityClearance = 2 can pass through door with requiredSecurity = 2
    boolean := doorLvL2.requestAccess(user2LvL2, 2);
    assert boolean;
    assert doorLvL2.doorState;
    assert !doorLvL2.alarmState;
    doorLvL2.closeDoor();
    assert !doorLvL2.doorState;
    
    //Assert that user with securityClearance = 3 can pass through door with requiredSecurity = 3
    boolean := doorLvL3.requestAccess(user3LvL3, 3);
    assert boolean;
    assert doorLvL3.doorState;
    assert !doorLvL3.alarmState;
    doorLvL3.closeDoor();
    assert !doorLvL3.doorState;
    
    //Assert that user with securityClearance = 3 can pass through door with requiredSecurity = 1
    boolean := doorLvL1.requestAccess(user3LvL3, 3);
    assert boolean;
    assert doorLvL1.doorState;
    assert !doorLvL1.alarmState;
    doorLvL1.closeDoor();
    assert !doorLvL3.doorState;
    
    //Assert that user with securityClearance = 1 can't pass through door with requiredSecurity = 2
    boolean := doorLvL2.requestAccess(user1LvL1, 1);
    assert !boolean;
    assert !doorLvL2.doorState;
    assert doorLvL2.alarmState;
    assert user1LvL1.token.valid;
    
    //Assert that user with securityClearance = 1 can't pass through door with requiredSecurity = 3
    boolean := doorLvL3.requestAccess(user1LvL1, 1);
    assert !boolean;
    assert !doorLvL3.doorState;
    assert doorLvL3.alarmState;
    assert user1LvL1.token.valid;
    
    //Assert that user with securityClearance = 2 can't pass through door with requiredSecurity = 3
    boolean := doorLvL3.requestAccess(user2LvL2, 2);
    assert !boolean;
    assert !doorLvL2.doorState;
    assert doorLvL2.alarmState;
    assert user2LvL2.token.valid;
    
    //Assert that user with other user's token can't pass through door with requiredSecurity = 1
    boolean := doorLvL1.requestAccess(user1LvL1, 2);
    assert !boolean;
    assert !doorLvL1.doorState;
    assert doorLvL1.alarmState;
    assert !user1LvL1.token.valid;
    
    //Assert that user with other user's token can't pass through door with requiredSecurity = 2
    boolean := doorLvL2.requestAccess(user2LvL2, 3);
    assert !boolean;
    assert !doorLvL2.doorState;
    assert doorLvL2.alarmState;
    assert !user2LvL2.token.valid;
    
    //Assert that user with other user's token can't pass through door with requiredSecurity = 3
    boolean := doorLvL3.requestAccess(user3LvL3, 1);
    assert !boolean;
    assert !doorLvL3.doorState;
    assert doorLvL3.alarmState;
    assert !user3LvL3.token.valid;
	}
}
 
